/*
 * Copyright (c) 2018-2020 Karlatemp. All rights reserved.
 * @author Karlatemp <karlatemp@vip.qq.com> <https://github.com/Karlatemp>
 * @create 2020/08/07 12:28:05
 *
 * YggdrasilOfficialProxy/YggdrasilOfficialProxy/build.gradle
 */

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}


import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

group = "io.github.karlatemp"
version = "2.3.0"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven' }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'
    implementation 'org.spongepowered:configurate-hocon:4.1.1'

    def ktor_version = "1.6.3"
    implementation "io.ktor:ktor-websockets:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-http-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    // compile "io.ktor:ktor-client-apache:$ktor_version"
    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    // https://mvnrepository.com/artifact/org.jline/jline
    implementation group: 'org.jline', name: 'jline', version: '3.20.0'
    // https://mvnrepository.com/artifact/org.fusesource.jansi/jansi
    implementation group: 'org.fusesource.jansi', name: 'jansi', version: '2.3.2'

}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
        freeCompilerArgs += '-Xopt-in=kotlin.contracts.ExperimentalContracts'
    }
}

tasks.create("standardShadowJar", ShadowJar.class) { task ->
    task.classifier("proxy")
    task.dependsOn('classes')
    task.exclude 'module-info.class'
    task.exclude 'META-INF/**.SF'
    task.exclude 'META-INF/**.RSA'
    task.from(file('build/classes/kotlin/main'))
    task.configurations = new ArrayList<>(
            Arrays.asList(project.configurations.getByName("runtimeClasspath"))
    )

    manifest {
        attributes([
                'Premain-Class'               : 'io.github.karlatemp.yop.YggdrasilOfficialProxy',
                'Main-Class'                  : 'io.github.karlatemp.yop.YggdrasilOfficialProxy',
                'Can-Set-Native-Method-Prefix': 'true',
                'Can-Retransform-Classes'     : 'true',
                'Can-Redefine-Classes'        : 'true'
        ])
    }
}
tasks.create("shadowPaperclip", ShadowJar.class) { task ->
    task.classifier("paperclip")
    task.dependsOn('classes')
    task.exclude 'module-info.class'
    task.exclude 'META-INF/**.SF'
    task.exclude 'META-INF/**.RSA'
    from(configurations.shade.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF', 'META-INF/**'
    }
    def relocateMap = { LinkedHashMap<String, String> args ->
        args.entrySet().each {
            task.relocate it.getKey(), 'io.github.karlatemp.yop.lib.' + it.getValue()
        }
    }
    task.from(file('build/classes/kotlin/main'))
    task.from(file('build/resources/main'))
    task.configurations = new ArrayList<>(
            Arrays.asList(project.configurations.getByName("runtimeClasspath"))
    )
    task.exclude 'META-INF/services/org.jline.terminal.spi.JnaSupport'
    task.exclude 'META-INF/services/org.jline.terminal.spi.JansiSupport'
    task.manifest {
        attributes([
                'Premain-Class'               : 'io.github.karlatemp.yop.YggdrasilOfficialProxy',
                'Launcher-Agent-Class'        : 'io.github.karlatemp.yop.OpenLibrary',
                'Main-Class'                  : 'io.github.karlatemp.yop.YggdrasilOfficialProxy',
                'Can-Set-Native-Method-Prefix': 'true',
                'Can-Retransform-Classes'     : 'true',
                'Can-Redefine-Classes'        : 'true'
        ])
    }
    task.dependencyFilter.exclude {
        println(it)
        it.moduleGroup == 'org.fusesource.jansi'
    }
    relocateMap([
            'okio'                     : 'okio',
            'okhttp3'                  : 'okhttp.okhttp3',
            'ninja.leaping.configurate': 'org.spongepowered.ninja.leaping.configurate',
            // kotlin.reflect.jvm.internal.KotlinReflectionInternalError: Unresolved class: class java.lang.String
            //'io.ktor'                  : 'io.ktor',
            //'kotlin'                   : 'kotlin',
            //'kotlinx'                  : 'kotlin',
            'org.checkerframework'     : 'org.checkerframework',
            'org.eclipse.jetty.alpn'   : 'org.eclipse.jetty.alpn',
            'com.typesafe'             : 'com.typesafe',
            'com.google'               : 'com.google',
            'org.slf4j'                : 'org.slf4j',
            'io.netty'                 : 'io.netty',
            'org.jline'                : 'org.jline'
    ])
}

shadowJar {
    classifier("minecraft")
    manifest {
        attributes([
                'Premain-Class'               : 'io.github.karlatemp.yop.YggdrasilOfficialProxy',
                'Main-Class'                  : 'io.github.karlatemp.yop.YggdrasilOfficialProxy',
                'Can-Set-Native-Method-Prefix': 'true',
                'Can-Retransform-Classes'     : 'true',
                'Can-Redefine-Classes'        : 'true'
        ])
    }
    exclude 'module-info.class'
    exclude 'META-INF/**.SF'
    exclude 'META-INF/**.RSA'
    def relocateMap = { LinkedHashMap<String, String> args ->
        args.entrySet().each {
            relocate it.getKey(), 'io.github.karlatemp.yop.lib.' + it.getValue()
        }
    }
    def excl = [
            'com.google.guava',
            'io.netty',
            'com.google.code.gson',
            'org.slf4j',
            'org.fusesource.jansi'
    ]
    dependencyFilter.exclude {
        if (excl.contains(it.moduleGroup))
            return true
        return false
    }
    exclude 'META-INF/services/org.jline.terminal.spi.JnaSupport'
    exclude 'META-INF/services/org.jline.terminal.spi.JansiSupport'
    relocateMap([
            'okio'                     : 'okio',
            'okhttp3'                  : 'okhttp.okhttp3',
            'ninja.leaping.configurate': 'org.spongepowered.ninja.leaping.configurate',
            'io.ktor'                  : 'io.ktor',
            // kotlin.reflect.jvm.internal.KotlinReflectionInternalError: Unresolved class: class java.lang.String
            // 'kotlin'                   : 'kotlin.kotlin',
            // 'kotlinx'                  : 'kotlin.kotlinx',
            'org.checkerframework'     : 'org.checkerframework',
            'org.eclipse.jetty.alpn'   : 'org.eclipse.jetty.alpn',
            'com.typesafe'             : 'com.typesafe',
            'org.jline'                : 'org.jline'
    ])
}
